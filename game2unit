#!/usr/bin/env nu

# Wrapper around `app2unit`
def --wrapped main [
	--help (-h) # Display the help message.
	...args # Arguments passed to `app2unit`
]: nothing -> any {
	exec app2unit ...$args
}

# Generic function for launcher subcommands
def --wrapped main_launcher [
	name: string # Name of the launcher
	extract: closure # Function that extracts the unit properties
	...args # Arguments passed to `app2unit`
]: nothing -> any {
	let options: oneof<list<string>, nothing> = do --capture-errors $extract
	if $options != null {
		(
			"game2unit => app2unit " + (
				$options
				| each { $"'($in)'" }
				| str join (char space)
			)
			| print --stderr $in
		)
		main ...$options ...$args
	} else {
		error make --unspanned {
			msg: $"environment is not ($name)",
		}
	}
}

# Wrapper around `app2unit` that extracts the unit properties from Steam.
def --wrapped "main steam" [
	--help (-h) # Display the help message.
	...args # Arguments passed to `app2unit`
]: nothing -> any {
	use launchers/steam.nu main

	main_launcher "Steam" {steam} ...$args
}

# Wrapper around `app2unit` that extracts the unit properties from [Minecraft] Prism Launcher.
def --wrapped "main minecraft-prism" [
	--help (-h) # Display the help message.
	...args # Arguments passed to `app2unit`
]: nothing -> any {
	use launchers/minecraft-prism.nu main

	main_launcher "[Minecraft] Prism Launcher" {minecraft-prism} ...$args
}
